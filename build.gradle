apply plugin: 'idea'

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
}

subprojects {
    apply plugin: 'java'

    group = 'com.batiaev.aiml'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
}

ext {
    junitVersion = '4.12'
    logbackVersion = '1.1.7'
    slf4jVersion = '1.7.21'
}

configure(project(':app-core')) {
    apply plugin: 'application'
    apply plugin: "jacoco"

    jacoco {
        toolVersion = "0.7.7.201606060606"
        reportsDir = file("$buildDir/reports/jacoco")
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "${buildDir}/reports/jacoco/html"
        }
    }

    test {
        jacoco {
            append = false
            destinationFile = file("$buildDir/reports/jacoco/test/jacocoTest.exec")
            classDumpFile = file("$buildDir/reports/jacoco/test/classpathdumps")
        }
    }

    mainClassName = "com.batiaev.aiml.App"

    jar {
        manifest.attributes("Main-Class": mainClassName);
        manifest.attributes("Manifest-Version": 1.0);
    }

    dependencies {
        compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        testCompile "junit:junit:$junitVersion"
    }

}